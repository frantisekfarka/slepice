grammar
Ty {{ tex \ttbase{T} }}, A {{ tex \ttbase{A} }}, B {{ tex \ttbase{B} }} :: 'ty_' ::= {{ com types }}
  | tcon                 ::   :: tcon                {{ com type constant }}
  | Pi Ty1 . Ty2         ::   :: pi_intro            {{ com type of dependent functions }}
  | Ty te                ::   :: pi_elim             {{ com application of a type former }}
  | ( Ty )               :: S :: paren                        
     {{ coq [[Ty]] }}
     {{ ocaml ([[Ty]]) }}

te {{ tex \ttbase{t} }}, M {{ tex \ttbase{M} }}, N {{ tex \ttbase{N} }} :: 'te_' ::= {{ com terms }}
  | con                  ::   :: con                 {{ com term constant }} 
  | ix                   ::   :: ix                  {{ com de Bruijn indices }} 
  | \ Ty . te            ::   :: pi_intro            {{ com formation of $\Pi$ type }}
  | te1 te2              ::   :: pi_elim             {{ com application }}
  | ( te )               :: S :: paren                        
     {{ coq [[te]] }}
     {{ ocaml ([[te]]) }}

grammar
K {{ tex \ttbase{K} }} , L {{ tex \ttbase{L} }} :: 'k_' ::= {{ com kinds }}
  | typeK                ::   :: type                {{ com kind of all types }} 
  | Pi Ty . K            ::   :: pi_intro            {{ com kind of dependent functions }}
  | ( K )                :: S :: paren                        
     {{ coq [[K]] }}
     {{ ocaml ([[K]]) }}

subrules
    K   <:: eK
    Ty  <:: eTy
    te  <:: ete

grammar
sgn {{tex \ttbase{Sig} }},
S {{ tex \ttbase{\mathcal{S} } }} {{coq Sgn }} :: 'sgn_' ::= {{ com signature }}
%     {{ phantom }}
    {{ coq list (con*{A : eTy | is_Ty_of_eTy A} + tcon*{L : eK  | is_K_of_eK L}) }}
    {{ ocaml (con * eTy , tcon*eK) sum  list }}
  | empty                ::   :: empty               {{ com empty signature }}
     {{ coq nil }}
  | sgn , con : Ty       ::   :: con
     {{ coq (cons (inl([[con]],[[Ty]])) [[sgn]]) }}
  | sgn , tcon : K       ::   :: tcon
     {{ coq (cons (inr([[tcon]],[[K]])) [[sgn]]) }}


grammar
ctx {{tex \ttbase{Ctx} }},
G {{ tex \ttbase{\Gamma} }} :: 'ctx_' ::= {{ com context }}
    {{ coq list ({A : eTy | is_Ty_of_eTy A}) }}
    {{ ocaml eTy list }}
%   defined after subrules
%    {{ phantom }}
  | empty                ::   :: empty              {{ com empty context }}
     {{ coq nil }}
  | ctx , Ty             ::   :: ctxvar
     {{ coq (cons [[Ty]] [[ctx]]) }}

subrules
    sgn <:: esgn
    ctx <:: ectx

grammar
sTy {{ tex {T^{-} } }},
tau {{ tex \tau }} :: 'sty_' ::= {{ com simple types }}
  | tcon                 ::   :: tcon                {{ com type constant }}
  | sTy1 -> sTy2         ::   :: pi_intro            {{ com type of functions }}
  | ( sTy )              :: S :: paren                        
     {{ coq [[sTy]] }}
     {{ ocaml ([[sTy]]) }}

grammar
sK {{ tex K^{-}{} }} ,
kappa {{ tex \kappa }} :: 'sk_' ::= {{ com simple kinds }}
  | typeK                ::   :: type                {{ com kind of all types }} 
  | sTy -> sK            ::   :: pi_intro            {{ com kind of functions }}
  | ( sK )               :: S :: paren                        
     {{ coq [[sK]] }}
     {{ ocaml ([[sK]]) }}

grammar
ssgn {{ tex \mathcal{S}^{-}{} }} :: 'ssgn_' ::= {{ com simple signature }}
    {{ coq list (con*sTy+tcon*sK) }}
%    {{ ocaml ((con * ty , tcon*k) sgn_el_type)  list }}
  | empty                ::   :: empty               {{ com empty signature }}
     {{ coq nil }}
  | ssgn , con : sTy     ::   :: con
     {{ coq (cons (inl([[con]],[[sTy]])) [[ssgn]]) }}
  | ssgn , tcon : sK     ::   :: tcon
     {{ coq (cons (inr([[tcon]],[[sK]])) [[ssgn]]) }}

grammar
sctx {{ tex Ctx^{-} }} , D {{ tex \Delta^{}{} }} :: 'sctx_' ::= {{ com simple context }}
    {{ coq list (sTy) }}
  | empty                 ::   :: empty              {{ com empty context }}
     {{ coq nil }}
  | sctx , sTy          ::   :: ctxvar
     {{ coq (cons [[sTy]] [[sctx]]) }}


embed
{{ coq 

Definition castSig (e : con*({eTy | is_Ty_of_eTy eTy})+tcon*({eK | is_K_of_eK eK}))
    := match e with
    | inl (c, ty) => inl (c, proj1_sig ty)
    | inr (beta, k) => inr (beta, proj1_sig k)
  end.

Definition fstSig (e : con*eTy + tcon*eK)
    := match e with
    | inl (c, ty) => inl c
    | inr (beta, k) => inr beta
  end.

(*
Definition boundCon c T0 S :=
  exists S1, exists S2,
    (List.map (castSig) S = List.app S1 (List.cons (inl(c,T0)) S2)) /\
    ~In (inl(c)) (List.map (fstSig) S1).

Definition boundTCon alpha T0 S :=
  exists S1, exists S2,
    (List.map (castSig) S = List.app S1 (List.cons (inr(alpha,T0)) S2)) /\
    ~In (inr(alpha)) (List.map (fstSig) S1).
*)
Definition boundCon c T0 S :=
  exists S1, exists S2,
    (S = List.app S1 (List.cons (inl(c,T0)) S2)) /\
    ~In (inl(c)) (List.map (fstSig) S1).

Definition boundTCon alpha T0 S :=
  exists S1, exists S2,
    (S = List.app S1 (List.cons (inr(alpha,T0)) S2)) /\
    ~In (inr(alpha)) (List.map (fstSig) S1).
}}
embed
{{ ocaml
(** val castSig : (con*eTy, tcon*eK) sum -> (con*eTy, tcon*eK) sum **)

let castSig = function
| Coq_inl p -> let c,ty = p in Coq_inl (c,ty)
| Coq_inr p -> let beta,k = p in Coq_inr (beta,k)

}}

